apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

version '0.5.4'
group 'org.zakariya'

publishing {
    publications {
        Production(MavenPublication) {
            artifact("$buildDir/outputs/aar/flyoutmenu-release.aar")
            groupId
            artifactId 'flyoutmenu'
            version this.version

            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')

                // Iterate over the implementation dependencies (we don't want the test ones), adding a <dependency> node for each
                configurations.implementation.allDependencies.each {
                    // Ensure dependencies such as fileTree are not included in the pom.
                    if (it.name != 'unspecified') {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_API_KEY')
    publications = ['Production']
    configurations = ['archives']
    override = true
    pkg {
        repo = 'maven'
        name = 'flyoutmenus'
        description = "Simple Material-style FlyoutMenu for Android"
        publish = true
        publicDownloadNumbers = true
        licenses = ['MIT']
        websiteUrl = 'https://github.com/ShamylZakariya/FlyoutMenus'
        vcsUrl = 'https://github.com/ShamylZakariya/FlyoutMenus.git'
        dryRun = false
        version {
            name = this.version
            desc = "Simple Material-style FlyoutMenu for Android"
            released = new Date()
            vcsTag = this.version
        }
    }
}